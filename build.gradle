plugins {
	// spring restdocs
	id "org.asciidoctor.jvm.convert" version "3.3.2"

	id 'java'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'HeoJin'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

// 쿼리 dsl 생성 경로 명시적으로  지정
def querydslDir = "src/main/generated"

// ./gradlew clean
// ./gradlew compileJava
configurations {
	// spring restdocs
	asciidoctorExt

	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	// json 직렬화 관련 (LocalDateTime)
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	compileOnly 'org.projectlombok:lombok'
	// 테스트 환경에서 lombok
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	// aop 관련 라이브러리
	implementation 'org.springframework.boot:spring-boot-starter-aop'

	// 마크다운 파서 관련
	implementation 'org.commonmark:commonmark:0.27.0'
	// 마크다운 안 url -> node 매칭
	implementation 'org.commonmark:commonmark-ext-autolink:0.27.0'
	
	// spring restdocs
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor:3.0.0'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc:3.0.0'


	// 운영 + test mysql
	runtimeOnly 'com.mysql:mysql-connector-j'

	// mongo 관련 의존성 추가
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	// jwt 관련
	implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'

	// 테스트만 h2 + mongo
	testImplementation 'com.h2database:h2'
	testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo.spring3x:4.21.0'

	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// QueryDSL 의존성
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
	annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api'


// cloudinary 관련 sdk
	implementation 'com.cloudinary:cloudinary-http44:1.34.0'

	// actuator 관련 의존성
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	// 프로메테우스 관련
	runtimeOnly "io.micrometer:micrometer-registry-prometheus"
}

// QueryDSL QClass 생성 설정
tasks.withType(JavaCompile) {
	options.getGeneratedSourceOutputDirectory().set(file(querydslDir))
}

// 소스 세트에 QueryDSL QClass 위치 추가
sourceSets {
	main.java.srcDirs += [ querydslDir ]
}

// clean 작업 시 생성된 QClass 삭제
clean {
	delete file(querydslDir)
}

// spring rest docs -> ext, test, asciidoctor
ext {
	snippetsDir = file('build/generated-snippets')
}

test {
	outputs.dir snippetsDir
}

asciidoctor {
	inputs.dir snippetsDir
	configurations 'asciidoctorExt'
	dependsOn test
}

// spring restdocs
bootJar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

tasks.named('test') {
	useJUnitPlatform()
}